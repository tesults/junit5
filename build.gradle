group 'com.tesults.junit5'
version '1.2.0'

apply plugin : "java"
apply plugin : "java-library"
apply plugin: "maven-publish"
apply plugin: "signing"

//apply plugin: "io.spring.dependency-management"


sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'com.tesults:tesults:1.0.3'
    implementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    implementation 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
    implementation 'org.junit.platform:junit-platform-launcher:1.4.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'tesults-junit5'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'tesults-junit5'
                description = 'Tesults JUnit5 Listener'
                url = 'https://www.tesults.com'
                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "ajeetd"
                        name = "Ajeet Dhaliwal"
                        email = "help@tesults.com"
                    }
                }

                scm {
                    url = "https://github.com/tesults/junit5.git"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}